import fuerzaOscura.*
import hechizos.*
import artefactos.*
import Personaje.*
import refuerzos.*
import Comerciante.*

describe "Test de comercio" {

	// HECHIZOS
	const hechizoDeLogos = new Logo(nombre = 'alacachula cachicomula', multiplo = 1)
	// ARTEFACTOS
	const espada = new Arma()
	const armaduraConCota = new Armadura(refuerzo = new CotaDeMalla(unidadLucha = 5), pesoArmadura = 0)
	const armaduraSinRefuerzo = new Armadura()
	const armaduraConAlacachula = new Armadura(refuerzo = hechizoDeLogos, pesoArmadura = 0)
	const armaduraConBendicion = new Armadura(refuerzo = bendicion, pesoArmadura = 0)
	const libroDeHechizos = new LibroDeHechizos(listaDeHechizos = [ hechizoBasico, hechizoDeLogos ])
	// PERSONAJES
	const thor = new Personaje(artefactos = [], hechizoPreferido = hechizoBasico, limiteDeCarga = 200)
	const loki = new Personaje(artefactos = [], monedasDeOro = 5, hechizoPreferido = hechizoBasico, limiteDeCarga = 200)
	const xenia = new Personaje(artefactos = [], hechizoPreferido = hechizoDeLogos, limiteDeCarga = 200)
	// COMERCIANTE
	const mockHnos = new Comerciante(tipoDeImpuesto = new ImpuestoDeComercianteIndependiente(comision = 0))

	fixture {
		collarDivino.perlas(5)
	}

	test "El valor de lista del hechizo de logos es de 22" {
		assert.equals(hechizoDeLogos.valor(), 22)
	}

	test "Si Xenia canjea su hechizo preferido por el hechizo basico, no se le descontara ninguna moneda" {
		xenia.compraA(mockHnos,hechizoBasico)
		assert.equals(xenia.monedasDeOro(), 100)
	}

	test "Si Thor canjea su hechizo preferido por el hechizo de logos, su monedas quedaran en 83" {
		thor.compraA(mockHnos,hechizoDeLogos)
		assert.equals(thor.monedasDeOro(), 83)
	}

	test "Si Thor canjea dos veces el hechizo basico, su monedas quedaran en 90" {
		thor.compraA(mockHnos,hechizoBasico)
		thor.compraA(mockHnos,hechizoBasico)
		assert.equals(thor.monedasDeOro(), 90)
	}

	test "Si Loki canjea su hechizo por el hechizo de logos, no podra" {
		const excepcionEsperada = new NoSePuedeComprarOCanjear('No te alcanzan las monedas de oro para adquirir el nuevo artefacto. Cumpli objetivos para aumentar tus monedas de oro.')
		assert.throwsExceptionLike(excepcionEsperada, { loki.compraA(mockHnos,hechizoDeLogos)})
	}

	test "Si Xenia compra una espada, un collar divino con cinco perlas, una armadura sin refuerzo y otra con cota de malla (con 5 unidades de lucha), tendra 70,5 monedas de oro" {
		xenia.compraA(mockHnos, espada)
		xenia.compraA(mockHnos, collarDivino)
		xenia.compraA(mockHnos, armaduraSinRefuerzo)
		xenia.compraA(mockHnos, armaduraConCota)
		assert.equals(xenia.monedasDeOro(), 70.5)
	}

	test "Si Xenia compra una espada, un collar divino con cinco perlas, una armadura sin refuerzo y otra con cota de malla (con 5 unidades de lucha), tendra 4 artefactos y no estara cargada" {
		xenia.compraA(mockHnos, espada)
		xenia.compraA(mockHnos, collarDivino)
		xenia.compraA(mockHnos, armaduraSinRefuerzo)
		xenia.compraA(mockHnos, armaduraConCota)
		assert.notThat(xenia.estasCargado())
	}

	test "Si Loki intenta comprar el espejo, no podra comprarlo ya que no tiene la cantidad suficiente de monedas. " {
		const excepcionEsperada = new NoSePuedeComprarOCanjear('No te alcanzan las monedas de oro para adquirir el nuevo artefacto. Cumpli objetivos para aumentar tus monedas de oro.')
		assert.throwsExceptionLike(excepcionEsperada, { loki.compraA(mockHnos,espejo)})
	}

	test "Si Thor compra una armadura con bendicion y otra que tiene como refuerzo el hechizo alacachula, tendra 74 monedas" {
		thor.compraA(mockHnos,armaduraConAlacachula)
		thor.compraA(mockHnos,armaduraConBendicion)
		assert.equals(thor.monedasDeOro(), 74)
	}

	test "Si Xenia canjea un libro de hechizos que contiene al hechizo de logos y el hechizo basico, tendra 69 monedas de oro" {
		xenia.compraA(mockHnos,libroDeHechizos)
		assert.equals(xenia.monedasDeOro(), 69)
	}

}

